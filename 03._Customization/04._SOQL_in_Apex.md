#### 1. What is the return type of a SOQL?
- The Return type of a SOQL depends on the keywords that you used in a SOQL
- Mainly a SOQL can Return
	1. List of sObjects (i.e. records)
	2. Integer
	3. sObject
	4. Array `AggregateResult[]`
	5. String
```apex
public class Demo {
    public static void soqlReturnsList(){
        List<Account> accList = [SELECT Id FROM Account LIMIT 10];
        System.debug(accList.size());
    }
    
    public static void soqlReturnsObject(){
        sObject accRecord1 = [SELECT Id FROM Account LIMIT 1];
        Account accRecord2 = [SELECT Id, Name FROM Account WHERE Id = '0012w00001TkCAYAA3'];
        System.debug(accRecord1);
        System.debug(accRecord2);
    }
    
    public static void soqlReturnsInteger(){
        Integer recordCount = [SELECT COUNT() FROM Account];
        System.debug(recordCount);
    }
    
    public static void soqlReturnsArray(){
        AggregateResult[] groupedResults
            = [SELECT CampaignId, AVG(Amount)
               FROM Opportunity
               GROUP BY CampaignId];
        for (AggregateResult ar : groupedResults)  {
            System.debug('Campaign ID' + ar.get('CampaignId')+'Average amount' + ar.get('expr0'));
        }
    }
    
    public static void soqlReturnsString(){
        String name = [SELECT Name FROM Account LIMIT 1].Name;
        System.debug('Name = '+name);
    }
}
```
___
#### 2. How to Store a SOQL in Set or Map?
Just write the SOQL in the Bracket `()` while declaring the Set or Map
```apex
Set<Account> accSet = new Set<Account>([SELECT Id, Name, Rating FROM Account]);

Map<Id, Account> accMap = new Map<Id, Account>([SELECT Id, Name, Rating FROM Account]);
```
___
### using for each loop vs standard loop to manipulate the Records
1. The for each loop is good for manipulating the Records if they are already present in the List or you have the SOQL inline of for loop.
```apex
// Itterating List or Set
List<String> studentName = new List<String>{'John', 'Mark', 'Henry'};
    for(String student : studentName){ // Fetches one record at a time
        System.debug('Name = '+student);
    }

// Itterating Map and accessing both key and values
Map<Integer, String> studentMap = new Map<Integer, String>{20 =>'John', 25 =>'Mark', 69 =>'Hery'};
    for(Integer rollNumber : studentMap.keySet()){
        System.debug('roll number = '+rollNumber+' and Name = '+studentMap.get(rollNumber));
    }
```
2. The standard for loop gives you a better control while creating new Records
```apex
public class Demo {
    public static void usingStandardLoop(){
        List<ParentB__c> parList = new List<ParentB__c>();
        for(Integer i=0;i<10;i++){
            ParentB__c record = new ParentB__c();
            record.Name__c ='Test'+i;
            parList.add(record);
        }
        insert parList;
    }
}
```
___
#### 3. What is a Binding Variable in SOQL
- We pass value in the `WHERE` clause in the SOQL to refine our SOQL so, that it only returns records that we want. ex: `SELECT Id, Gender__c FROM Candidate__c WHERE Gender__c = 'MALE'`
- But instead of Passing the value directly if we want to use a Variable or Collection as Value then we reference that Variable or Collection in our SOQL and this reference is called as Binding Variable. The Binding Variable is always preceded by a colon `:`.
- he Apex parser first evaluates the local variable in code context i.e. if the Bind Variable exists or not before executing the SOQL or SOSL statement.

When to use `=`, `=:` `IN :`
1. Use `=` to give direct values i.e. no Binding Variable
```apex
public class BindingVariables {
    Static List<Account> accList = new List<Account>();
    public Static void displayAccountInfo(){
        for(Account acc : [SELECT Id, Name, Phone FROM Account WHERE Id ='0012w00001CebxdAAB']){
            accList.add(acc);
        }
        System.debug(accList.size());
    }
}
```
2. Use `=:` to access value from a variable
```apex
public class BindingVariables {
    static Id accId ='0012w00001CebxdAAB';
    
    static List<Account> accList = new List<Account>();
    public static void displayAccountInfo(){
        for(Account acc : [SELECT Id, Name, Phone FROM Account WHERE Id =:accId]){
            accList.add(acc);
        }
        System.debug(accList.size());
    }
}
```
3. Use `IN :` to access value from a List or Set or `Map.keySet()` or `Map.values()`.
```apex
public class BindingVariables {
    static List<String> nameList = new List<String>{'GenePoint', 'United Oil & Gas, UK', 'United Oil & Gas, Singapore', 'Edge Communications'};
    static List<Account> accList = new List<Account>();
    public static void displayAccountInfo(){
        for(Account acc : [SELECT Id, Name, Phone FROM Account WHERE Name IN : nameList]){
            accList.add(acc);
        }
        System.debug(nameList.size() == accList.size());
    }
}
```
Bind expressions can be used as:
- The search string in FIND clauses. (SOSL)
- The filter literals in WHERE clauses.
- The value of the IN or NOT IN operator in WHERE clauses, allowing filtering on a dynamic set of values. Note that this is of particular use with a list of IDs or Strings, though it works with lists of any type.
- The numeric value in LIMIT clauses.
- The numeric value in OFFSET clauses.
___
#### 4. How to fetch Deleted Records in SOQL
- On every Object there is a Field `isDeleted` which is set to true as soon as the record is Deleted and is in the Recycle Bin.
- So, we write `isDeleted =true` after `WHERE` and append `ALL ROWS`
```apex
public class Demo1 {
    static List<Account> accList =[SELECT Id, Name FROM Account WHERE Name LIKE 'Demo%' AND isDeleted=true ALL ROWS];
    public static void display(){
        for(Account acc : accList){
            System.debug(acc.Name);
        }
        System.debug(accList.size());
    }
}
```
if you don't write `isDeleted =true` then all the records i.e. `database + recycleBin` will be fetched.
___
### Relationship Queries
If you want to fetch Records from 2 different object using 1 SOQL it's Mandatory to have a Relationship between them.
#### 5. How do we write a Parent to Child SOQL
- We write Inner Query for the Child sObject which is related to Parent sObject. Hence, to fetch the Inner Object records we will write a nested for Loop.
- In the inner Query we don't write the sObject name of Parent after the `FROM` keyword instead we need to Write the Child Relationship Name.
Ex: For each Account the nested for loop will run for the Contacts it has.
```apex
public class RelationshipQueries {
    public void parentToChild(){
        List<Account> accList = new List<Account>();
        accList = [SELECT Id, Name , (SELECT Id, lastName, Phone FROM Contacts) FROM Account WHERE Id IN (SELECT AccountID FROM Contact)]; /* I used the SOQL on Contact in WHERE clause so that only such Accounts will be fetched that has atleast 1 Contact */
        
        for(Account acc : accList){
            System.debug('Account Name = '+acc.Name);
            for(Contact con : acc.Contacts){	// This is how you access the inner query
                System.debug('Contact Name ='+con.lastName);
            }
        }
    }
}
```
**Limitations of Parent to Child SOQL:**
- No more than 20 parent-to-child relationships can be specified in a query i.e. You can have 20 nested sub-queries. ex: Account is Parent of Opportunity and Contact so you can have 1 inner query for each. i.e. you can fetch 20 Child objects for 1 Parent Object in 1 query.
- Aggregate functions, such as SUM(), COUNT(), and MAX(), cannot be used in Parent-to-Child SOQL queries.
-  **You cannot have a inner query inside a inner query** i.e. Parent to child supports only 1 level deep query.
____
#### 6. How to get the Child Relationship name for the Parent to Child inner Query
- To get the relationship name : Object Manager → Child sObject → Fields and Relationships → Find a Field under DataType which is MDR or Lookup ex: Lookup(Account) Click on that field → Copy Child Relationship Name.
- Process is same for both Standard and Custom fields only difference is that if the Child Object is a Custom then you need to append `__r` in the Child Relationship Name.
```soql
SELECT Id, Name, (SELECT Name, Amount, StageName FROM Opportunities) FROM Account

SELECT Id, First_Name__c, Gender__c, (SELECT Id, City__c FROM addresses__r
) FROM Applicant__c
```
**NOTE** :
- You get Relationship Name from Child Object because you always create a Relationship field on a Child Object.
- Generally the Relationship Name for Parent to Child traverse is just the Plural name of the Child Object's API name. (Hence, whenever you create a Custom Object always write the API name as Singular the Tab Name can be and technically should be Plural as the Tab holds multiple Records for that Object)
____
#### 7. How do we write a Child to Parent SOQL
- The Relationship Name for the Child to Parent is the API Name of the Relationship Field . 
- **Process** : Setup &rarr; Object Manager &rarr; Select the Child Object &rarr; Select the Relationship Fields Name and if this a Custom Relationship Field then Append `__r`.
- You can directly Write the Parent name and use a dot to access its Fields ex: `Account.Name`
```apex
public class RelationshipQueries {
    public void childToParentStandardRelationship(){ // the relationship was created by salesforce
        List<Contact> conList = new List<Contact>();
        conList = [SELECT Id, lastName, Account.Name FROM Contact LIMIT 25];
        
        for(Contact con : conList){
            System.debug('Contact Name = '+con.lastName+' Account Name = '+con.Account.Name); // This is how you access the Parent fields
        }
    }

    public void childToParentCustomRelationship(){ // the relationship was created by you
    	List<Child_Object__c> childList = [SELECT Id, Child_Object__c.Parent__r.Id, Child_Object__c.Parent__r.Name__c FROM Child_Object__c];
        for(Child_Object__c rec : childList){
            System.debug('Child Id'+rec.Id+' Parent Name'+rec.Parent__r.Name__c);
        }
    }
}
```
**Limitations of Child to Parent SOQL :
- Child to Parent support up-to 5 level deep query. (A Child must be having a Parent that Parent might be a Child of some Other Parent and so on). For example, `Contact.Account.Owner.FirstName` (three levels).
- No more than 55 child-to-parent relationships can be specified in a query. A custom object allows up to 40 relationships, so you can reference all the child-to-parent relationships for a custom object in one query.
___
#### 8. Can you write both Parent to Child and Child to Parent relationship query in a Single SOQL
- Yes, so first you need such sObject which has 
	1. A Parent
	2. A Child

```apex
public class Demo {
    public static void together(){
        // This SOQL only fetches such Contacts that has both Parent Account and a Child Case
        List<Contact> conList = [SELECT ID, Name, Account.Name, AccountId, Account.Rating,(SELECT ContactId, CaseNumber, Subject FROM 
                                                                                           Cases) FROM Contact WHERE AccountId != null AND Id IN (SELECT ContactId FROM Case)];
        
        for(Contact conRec : conList){
            System.debug('Parent Name ='+conRec.Account.Name+' current record name ='+conRec.Name);
            for(Case caseRec : conRec.Cases){
                System.debug('Case Number ='+ caseRec);
            }
        }
    }
}
```
____
#### 9. When to Use SOQL
1. when you know which objects the data resides in
2. Retrieve data from a single object or from multiple objects that are related to one another.
3. Count the number of records that meet specified criteria.
4. Sort results as part of the query.
___
#### 10. How to fetch Multi-Select Picklist Field such that it contains certain Values
As we know a Multi-Select Picklist Field has multiple values so the condition in WHERE clause is different
```sql
SELECT Gender__c FROM Candidate__c WHERE Gender__c includes ('Other;Male','Female')
/* Here the SOQL will fetch such Candidate Records where the Gender field has
1. Both Other and Male Selected
2. Only Female is Selected
*/
```
- `includes` - only show records that have the given Multiselect Picklist values
- `excludes` - only show records that don't have the given Multiselect Picklist values
- `=` - `Gender__c = 'Other;Male'` to pass 1 Set of values. Here if you use `!=` those records will be fetch that does not have the provided values.
___
#### 11. SOQL For Loops Versus Standard SOQL Queries
```apex
public class Demo {
    public static void usingStandardSOQL(){
        List<ParentB__c> parList = [SELECT Name__c FROM ParentB__c WHERE Name__c ='John'];
        /* All Records Stored and fetched i.e. 50,000 records fetched */
        System.debug('Size of List ='+parList.size());
        Integer i =0;
        for(ParentB__c record : parList){
            /* As we are using the loop 1 Record is manipulated at a Time here you should not write the 
DML inside the Loop because 1 DML will process 1 Record and 150 DML's will process 150 Records so, you
wasted not just DML's but you processed very less records because 1 DML can process 10,000 Records */
            i++;
        }
        /* You write DML here i.e. Outside the For Loop so, now you will only use 1 DML and it will Process
10,000 Records. But if your List contains more that 10,000 Records as you know SOQL can fetch 50,000 Records
So, your DML will fail with an Error System.LimitException: Too many DML rows */
        System.debug('The for loop Ran'+' '+i+' times');
    }
    
    public static void usingSoqlForLoopListVariable(){
        Integer i=0;
        Integer j=0;
        for(List<ParentB__c> parList : [SELECT Name__c FROM ParentB__c WHERE Name__c ='John']){
            /* Here We are using a List Variable so the SOQL is using `query` and `queryMore` methods of SOAP API.
and the List Variable will contain 200 Records at a Time so, you need to have one more for loop on that List Variable
so, that you can manipulate the Records */
            System.debug('Size of List ='+parList.size()); // This will have 200 Records per Loop
            for(ParentB__c rec : parList){
                j++; /* Here you process 1 Record at a Time and Hence, this loop will Run 200 Times for 
1 Run of the List Variable
i.e. 1 Run of parList = 200 Runs of rec * /
}
i++; /* Replace this with DML. 1 DML will run for 200 Records i.e. 150 DML
will process 30,000 Records*/
            }
            System.debug('i ='+i+' and j ='+j);
        }
        
    }
    
    public static void usingSoqlForLoopObjectVariable(){
        Integer i =0;
        for(ParentB__c rec : [SELECT Name__c FROM ParentB__c WHERE Name__c ='John']){
            /* Records are fetched but as we are using Object Variable it's Processing 1 Record at a Time
just like what we did in Standard SOQL 
So, here alos you should not do DML inside for Loop */
            i++;
        }
        System.debug('The for loop Ran'+' '+i+' times');
    }
}
```
- We know that we have Total heap size Governor Limit i.e. 6MB for Sync and 12MB for Async. And the Heap is for all the Variables that are created in the entire Apex. So, when we use the Standard SOQL Queries and the received record rows are huge then it can sometimes have Heap size Error.
- The SOQL For Loops work in different way. SOQL for loops retrieve all sObjects, using efficient chunking with calls to the `query` and `queryMore` methods of SOAP API.
- TYPE 1 List Variable stores SOQL : The sObject list format executes the for loop's `<code_block>` one time per list of 200 sObjects i.e. it processes 200 Records at a Time. So, if you want to use DML inside the for loop this will be good for you.
- TYPE 2 Object Variable stores SOQL : The single sObject format executes the for loop's `<code_block>` one time per sObject record i.e. it Processes 1 record at a Time. t’s easy to understand and use, but is grossly inefficient if you want to use data manipulation language (DML) statements within the for loop body. Each DML statement ends up processing only one sObject at a time.
**NOTE** : We have read a Lot of Times don't use for loop inside another for loop as it is CPU intensive Task. Don't use DML statements inside the for Loop as it Consumes multiple DML Limits. But, at the end it all comes down to what your working in i.e. Sync vs Async; is it a Trigger or just a Standalone Class and what are you trying to achieve. Sometimes you need to cut corners. 
**Proof**: [SOQL For Loops Documentation](https://developer.salesforce.com/docs/atlas.en-us.244.0.apexcode.meta/apexcode/langCon_apex_loops_for_SOQL.htm)
___
### Complex SOQLs
Till now we saw how to write SOQL on 2 Objects that are Related to Each Other. But things get complex as more Objects come into play.
____
