#### 1. What is Apex Testing?
Apex testing means Testing if a Apex Code is working as expected or not.
___
#### 2. Why do Apex Testing?
1. Salesforce recommends that you have a Test-Driven Deployment Process i.e. While the Apex is being Developed you should do the testing of it simultaneously.
2. Having a Test Class enables you to quickly verify if your Apex Code is working or not after you have modified it.
3. Before you can deploy your code to the Production Environment or package it for the Salesforce AppExchange, the following conditions should be met otherwise the Deployment won't happen :
	1. Unit tests must cover at least 75% of your Apex code, and all of those tests must complete successfully. Here, Apex Code means all Apex Classes in your Sandbox Collectively.
	2. Every trigger must have some test coverage.
	3. All classes and triggers must compile successfully.
- **NOTE** :
	1. It's mandatory that Test Coverage must be 75% for all Apex Combined in your Org. But, you should always aim that your each Apex Code itself gets 100% Test Cases Covered and has 100% Test Coverage.
	2. Test methods and test classes aren’t counted as part of Apex code coverage.
	3. Apex Test Classes can only be written in Sandbox Environments and Developer Org i.e. you cannot write Test Class in a Production Environment.
___
#### 3. What am I supposed to Test in an Apex Code?
You are supposed to Test any Apex Code/ Trigger for the following Test Cases.
1. Single Action : Test to verify that a single record produces the expected result or not.
2. Bulk Actions : Test to verify that when the same Apex Code or Trigger receives more than 1 Record then the Code produces the expected results or not.
3. Positive Behavior : We have if Conditions in the Apex ex: `if(!String.isBlank(conRecord.Email))`. Test if the user provides / Records have all the Conditions then the Apex code produces the expected result or not.
4. Negative Behavior : We have if Conditions in the Apex ex: `if(!String.isBlank(conRecord.Email))`. Test if the user does not provide / Records does not met the Conditions then how does the Apex code react i.e. verify that the error messages are correctly produced.
5. Restricted User : Suppose a user who has limited access to a sObject then how does the Apex react to the Operations done by this Restricted User.
_____
#### 4. What is a Test Class? What is an Apex Unit Test?
- Unit tests are class methods (in a Test Class) that verify whether a particular piece of code is working properly.
	1. Unit Tests methods must be annoted with `@isTest`.
	2. Unite Test methods can be can be Private or Public so, Access Modifiers don't matter.
	3. Unit Test methods must be `static`.
	4. Unit test methods take no arguments.
	5. Unit test methods commit no data to the database.
	6. Unit test methods can't send mails.
	7. Methods of a test class can only be called from a test method or code invoked by a test method; non-test requests can’t invoke it.
- A Test Class is just like other classes but
	1. A Test Class must be annoted with `@isTest`.
	2. A Test Class can be Private or Public so, Access Modifiers don't matter.
	3. Classes defined as `@IsTest` must be top-level classes and can't be interfaces or enums.

```apex
// Syntax

@isTest /* This states that this is a Test class */
private class TestClass { /* The Test Class is Private but it does not matter */
    
    @isTest /* This states that this is a Unit Test method */
    static void test1(){ /* NOTE: The Unit Test methods must be static and should not accept any arguments */
        /* If your Apex Classes method is not static then you must create an Instance to acces it */
    }
    
    @isTest /* Each Unit Test method has to be annoted by @isTest */
    static void test2(){ /* Here we test another functionality for the Same Apex Class */
        
    }
}
```
- **NOTE** : 
	2. You can Create Apex Unit Test methods in a Test class only i.e. a method annoted with `@isTest` can only  be Created in a Class which is annoted with `@isTest`.
	4. If your Apex Class has Private or Protected members then they can't be accessed from Outside of that Class so, Test Class also won't be able to access them so you need to Write `@TestVisible` for each Private or Protected Class Member so, the Test Class can access it.
	5. Calls to System.debug aren’t counted as part of Apex code coverage. So, Don't even try to cover the Scenario where output is a `System.debug();`.
	6. If the value of a static member variable in a test class is changed in a `testSetup` or test method, the new value isn’t preserved. Other test methods in this class get the original value of the static member variable. This behavior also applies when the static member variable is defined in another class and accessed in test methods. In simple words `static` does not hold its value across different Unit Test Methods. 
____
#### 5. Explain Test Data.
1. Test Data is just data that we create in a Test Class to check if the Apex Classes methods are working as expected or not.
2. Apex test data is transient and isn’t committed to the database i.e. any modification done by an Apex Unit Test is not persistent.
3. If your Apex Test Class is accessing the Org's data and performing some operations then also, the changes done are Transient i.e. not persistent.
4. By default, existing organization data isn’t visible to Test Classes, with the exception of certain setup objects. To make the Org's data (Records) visible to the Test Class instead of using `@isTest` annotation for the Apex Test Class use `@isTest(SeeAllData=True)`.
5. `@isTest(SeeAllData=True)` only works with Queries i.e. it only shows the Org's data to the Test Classes but, still all operations done by the Apex Unit Tests on the Org's Data will be Transient i.e. not persistent.
- **NOTE** :
	1. It is recommend by Salesforce that you should always create Test Data and don't use `@isTest(SeeAllData=True)`.
	2. You can use `@isTest(SeeAllData=True)` on the Test Class so that all the Apex Unit Tests can access the Org's data or you can use `@isTest(SeeAllData=True)` on Specific Apex Unit Test methods so that only the specific method has access to Org's Data.
_____
#### 7. What is a Static Resource?
1. Static Resources are just Files that you uploaded as Static Resources and now can be reference in places like : Test Classes and VisualforcePage's.
2. **Process** : Setup &rarr; Static Resource &rarr; New &rarr; Select Name, Description, File from Local Device and Cache Control.
3. Cache Control levels
	1. Private : The static resource is stored in cache only for the current user’s session.
	2. Public : The static resource data cached on the Salesforce server is shared with other users in your organization for faster load times. After it’s cached, the resource is accessible to all Internet traffic, including unauthenticated users.
4. You can upload any type of file in Static Resources.
5. These Static Resources can only be referred within Salesforce.
**NOTE** : 
1. You can Edit/ Delete the Static Resource. Edit will give you option to upload newer version of that File.
2. If you are uploading a `.csv` comma delimited file for a Particular sObject then
	1. The Top Row must have the Field Names.
	2. All Records must have data in Required Fields i.e.e Required Fields Cannot be empty.
____
#### 8. Instead of Creating Test Data manually using Code how can you use a Data of a `.csv` file?
1. The Test Class has a `loadData()` method it takes 2 Arguments
	1. `sObjectName.sObjectType`
	2. Static Resource Name in String Format.
2. The `loadData()` method returns `List<sObjects>`.
```apex
@isTest
private class DemoTest {
    
    @isTest
    private static void usingStaticResource(){
        /* Saving Test.loadData() in accList */
        List<Account> accList = Test.loadData(Account.sObjectType, 'AccountTestData'); /* Test.loadData(sObjectName.sObjectType, 'Static Resource Name as a String') */
        System.assertEquals(19,accList.size()); /* This will return True as the Static Resource has 19 records */
    }
}
```
____
#### 9. How to create Test data via code in Test Classes?
- We can create Records from the Apex Classes via Code similarly, just do the same in the Text Apex Classes to create Test Data.
- Additionally, what you can do instead of writing code to create Test Data for each Apex Class what you can do is create a Public Test Class and inside it create a Method that can called from other Test Classes for Test Data Creation.
- Such Public Test Classes are called Common/Public Test Utility Classes.
- The Public Test Class must be `public` or `global` same goes for the methods in it.
- There are some differences in Public Test Utility Classes and Normal Text Classes.
	1. The methods can accept Arguments. But it its accepting Arguments then it cannot be a `@isTest` method.
	2. The method can have a return type other than `void`.

```apex
// Apex Class

public class Demo{
    public static void changeRating(){
        List<Account> accList = new List<Account>(); /* Creating Empty List to Store Account Records which have Rating=null */
        accList = [SELECT Id, Name, Rating FROM Account WHERE Rating =null LIMIT 100]; /* Fetching at max 100 Records */
        
        if(!accList.isEmpty()){
            for(Account accRecord : accList){ /* Itearting over accList */
                accRecord.Rating ='Cold'; /* Chaning the Rating from null to Cold */
            }
            
            update accList; /* Committing the Changes to Salesforce Database */
        }
    }
}

// Public Utility Class that created the Test Data

@isTest
public class PublicTestUtility {
    
    /* The method is not marked with @isTest because its accepting Arguments while @isTest methods i.e. Apex Unit Test Methods can't take any Arguments and can only return void */
    /* NOTE: The below method can only be called from a @isTest method, the Below method won't commit any Records in the Database as this is being called by a @isTest method.
The below method is not annoted by @isTest but this won't use your orgs 1Million Char Limit because the Class has @isTest Annotation */
    public static void createAccountRecords(Integer numOfAccounts){ /* Accepts Integer and creates records accordingly */
        List<Account> accList = new List<Account>(); /* Creating Empty List to Store newly created Account Records */
        
        for(Integer i=0;i<numOfAccounts;i++){
            Account accRecord = new Account(Name ='TestClassRecord'+i);
            accList.add(accRecord); /* Adding Records to accList */
        }
        
        if(!accList.isEmpty()){
            insert accList; /* This won't commit to the Salesforce Database */
        }
    }
}

// Test Class for our Apex Class

@isTest
private class DemoTest {
    
    @isTest
    private static void changeRating(){
        PublicTestUtility.createAccountRecords(100); /* Calling the createAccountRecords from the PublicTestUtility class which handels the Test Data Creation */
        
        Test.startTest();
        Demo.changeRating(); /* Calling the method of Demo class that we want to Test */
        Test.stopTest();
        
        /* The below SOQL fetches the Test Data that we created using the  PublicTestUtility class */
        List<Account> updatedAccounts = [SELECT Id, Name, Rating FROM Account WHERE Name LIKE 'TestClassRecord%' AND Rating ='Cold'];
        System.assertEquals(100, updatedAccounts.size()); /* Checking if the List size is 100 because we only modified 100 records */
        
        for(Account accRecord : updatedAccounts){
            System.assertEquals('Cold', accRecord.Rating); /* Checking Rating of each Record */
        }
    }
}
```
- **NOTE** : 
	1. The methods of a Public Test Utility Class can only be called From a Test Class and cannot be called from a non-Test Class.
	2. The Public Test Utility Class is also annoted by `@isTest`. The methods if accepting arguments then we don't annotate them with `@isTest`.
_____
#### 10. What is `@testSetup` methods?
1. We saw how Public Test Utility Classes works for Creating Test Data that can be accessed by other Test Classes.
2. Similarly we can create Test Data in a Test Class that can be used by all the Apex Test Unit methods. The method in which we create Test Data must be annoted by `@testSetup`.
3. The Test Data created by the `@testSetup` is accessible by all Apex Test Unit methods of that Test Class.
4. If a Apex Test Unit method modifies the Test Data it will be rolled back to the original data after the execution of that method is finished i.e. the next Apex Unit Test method gets the fresh/ original Test Data.
5. You can have only one test setup method per test class.
6. If a test setup method calls a non-test method of another class, no code coverage is calculated for the non-test method i.e. Code coverage can only be achieved in the Apex Unit Test methods.

```apex
// Apex Class

public class Demo{
    public static void changeRating(){
        List<Account> accList = new List<Account>(); /* Creating Empty List to Store Account Records which have Rating=null */
        accList = [SELECT Id, Name, Rating FROM Account WHERE Rating =null LIMIT 100]; /* Fetching at max 100 Records */
        
        if(!accList.isEmpty()){
            for(Account accRecord : accList){ /* Itearting over accList */
                accRecord.Rating ='Cold'; /* Chaning the Rating from null to Cold */
            }
            
            update accList; /* Committing the Changes to Salesforce Database */
        }
    }
}

// Apex Test Class

@isTest
private class DemoTest {
    
    @testSetup /* Creating a testSetup method which will create Test Data that can be accessd by any method in this Test Class */
    private static void createTestData(){
        List<Account> accList = new List<Account>();
        for(Integer i=0; i<100; i++){
            Account accRecord = new Account(Name ='Test Record'+i);
            accList.add(accRecord);
        }
        
        if(!accList.isEmpty()){
            insert accList;
        }
    }
    
    @isTest
    private static void testChangeRating(){
        Demo.changeRating(); /* Calling the method of Apex Class that we want to Test */
        
        List<Account> accList = new List<Account>(); /* Creating an Empty List to store accounts fetched by soql */
        accList = [SELECT Id, Name, Rating FROM Account WHERE Rating ='Cold' LIMIT 100]; /* This will fetch accounts from the Test Data that we created in testSetup */
        
        System.assertEquals(100, accList.size());
        
        for(Account accRecord : accList){
            System.assertEquals('Cold', accRecord.Rating);
        }
    }
}
```

_____
#### 11. How to Run a Unit Test Methods?
We can run Apex Unit Test methods in 4 ways
1) Developer Console &rarr; Test
2) Setup &rarr; Apex Test Execution &rarr; Select Tests &rarr; Select Namespace and Apex Test Classes to run i.e. here you cannot select individual Apex Unit Tests.
3) Visual Studio Code
4) API calls
____
#### 12. What are Different options to Run tests in Developer Console.
1. Always Run Asynchronously : This option ensures that all Apex Tests will run Asynchronously. The default behavior is as noted below
	1. Synchronous : If you are executing a Single Test Class/ Test methods from a single Test Class.
	2. Asynchronous : if you are executing Test methods from multiple Test Classes OR you are using Apex Test Execution to run tests.
2. New Run : This gives you option to select
	1. Namespace : 
		1. My Namespace : Select Test Classes from your org.
		2. All Namespaces : Select Test Classes from the org and installed Packages.
	2. Select all or some methods from multiple Test Classes i.e. from new Run you can select which Apex Unit Test you want to run.
3. Rerun : Executes your last Tests again.
4. Rerun Failed Tests : Will Execute all the Failed Tests.
5. Run All : Will run all Test Classes that you have.
6. New Suite : Here you can bunch together the Apex Unit Tests that you want to execute together whenever you want.
7. Suite Manager : Lets you Delete, Edit your Test Suites.
8. New Suite Run : Select the Suites that you want to Run.
______
#### 13. How governor Limits work in Test Classes?
1. As we saw above mostly Test Classes run Asynchronously and sometimes they run Synchronously, and you can even enable the option to always run tests Asynchronously. 
2. While running a Test the Governor Limits are shared by the Apex Unit Test methods, `@testSetup` methods and the Apex Method is called by the Apex Unit Tests.
3. So, you can see the Governor Limits might come to the threshold. To deal with situation we have some methods of Limits class and 2 methods of Test Class.
4. `Test.startTest()`
	1. This marks the beginning of Actual test i.e. the `@testSetup` method or Test Data creation must be done before the `Test.startTest()` method and after this the Calling of the Apex method that you want to test should be done.
	2. All the Operations that are performed between `Test.startTest()` and `Test.stopTest()` run in a seperate context i.e. they get seperate Governor Limits than that of the Test Class. So, this can help you in testing how much Governor Limit is consumed by the Apex Code that you are Testing.
5. `Test.stopTest()` 
	1. Marks the point in your test code when your test ends.
	2. After this you need to write the assert statements. So, that all the operations between the `Test.startTest()` and `Test.stopTest()` will be completed and the assert will have the data to check.
6. Limits - The Limits class contains methods that returns the specific governor Limits.

| Limits Methods           | Description                                                                                                                         |
| ------------------------ | ----------------------------------------------------------------------------------------------------------------------------------- |
| `getDMLRows()`           | Returns the number of records that have been processed by 1 DML statement.                                                          |
| `getLimitDMLRows()`      | Returns the Governor Limit of the total DML operations i.e. the total number of records that can be processed with 1 DML statement. |
| `getDMLStatements()`     | Return the number of DML statements that we used.                                                                                   |
| `getLimitDMLStatement()` | Returns the the Governor Limit of the total DML Statements i.e. the total number of DML statements that can be called.              |
| `getQueryRows()`         | Returns the number of records that have been fetched by 1 SOQL statement.                                                           |
| `getLimitQueryRows()`    | Returns the Governor Limit of the total SOQL operations i.e. the total number of records that can be Fetched with 1 SOQL statement. |
| `get.Queries()`          | Return the number of SOQL statements that we used.                                                                                  |
| `get.LimitQueries()`     | Returns the the Governor Limit of the total SOQL Statements i.e. the total number of SOQL statements that can be called.            |

- Example:

```apex
@isTest
private class DemoTest {
    
    @testSetup /* Creating a testSetup method which will create Test Data that can be accessd by any method in this Test Class */
    private static void createTestData(){
        List<Account> accList = new List<Account>();
        for(Integer i=0; i<100; i++){
            Account accRecord = new Account(Name ='Test Record'+i);
            accList.add(accRecord);
        }
        
        if(!accList.isEmpty()){
            insert accList;
        }
    }
    
    @isTest
    private static void testChangeRating(){
        /* We write Test.startTest before we call the Apex Method that we want to test */
        Test.startTest();
        Demo.changeRating(); /* Calling the method of Apex Class that we want to Test */
        Test.stopTest();
        /* We write Test.stopTest just after we called the Apex Method that we want to test. And after the Test.stopTest we have our assert statements */
        
        List<Account> accList = new List<Account>(); /* Creating an Empty List to store accounts fetched by soql */
        accList = [SELECT Id, Name, Rating FROM Account WHERE Rating ='Cold' LIMIT 100]; /* This will fetch accounts from the Test Data that we created in testSetup */
        
        System.debug('DML Rows ='+Limits.getDmlRows()); /* We can store the Limits.getDmlRows() in an Integer variale this is how we veify how must governor limit is being used. Here its testing the governor limit for the Test Class excluding the Code between the Test.startTest and Test.stopTest because the code between that runs with seperate Governor Limits */
        System.assertEquals(100, accList.size());
        
        for(Account accRecord : accList){
            System.assertEquals('Cold', accRecord.Rating);
        }
    }
}
```

- **NOTE** : 
	1. In any Apex Unit Test method you can only use `Test.startTest()` and `Test.stopTest()` only once.
	2. Any Test class either running Synchronous or Asynchronous still gets the Synchronous Governor Limits. So, Asynchronous execution of Apex Tests only delay them until resources are available they don't give it a Asynchronous Limit. 
_____
## Practical
**NOTE** : Whenever you edit an Apex Code or its Test Class the Code Coverage becomes 0% until you run the Test Class again and the tests pass.
#### 1. Write a Test Class for the Apex Class.

```
// Apex Class

/* If the Contacts has a Phone and AccountId then Account's Description = Contact's Phone */
public class UpdateAccountDescription {
    List<Contact> conList = new List<Contact>(); /* Creating Empty List to store Contact Records that have a Phone and a Parent Account */
    Set<Id> accIdSet = new Set<Id>(); /* Creating an Empty Set to store parent Account Id's */
    Map<Id, Account> accMap = new Map<Id, Account>(); /* Creating an Empty map to match the Child with Parent Account */
    
    public void populateAccountDescription(){
        conList = [SELECT Id, AccountId, Phone FROM Contact WHERE AccountId !=null AND Phone !=null]; /* Fetching Contact Records */
        
        
        for(Contact conRecord : conList){ /* Iterating the conList to populate the AccountId's in accIdSet */
            if(conRecord.AccountId !=null){
                accIdSet.add(conRecord.AccountId); /* Adding AccountId's in accIdSet */
            }
        }
        
        if(!accIdSet.isEmpty()){
            List<Account> accList = [SELECT Id, Name, Description FROM Account WHERE Id IN: accIdSet]; /* Fetching the Parent Accounts for the Contacts */
            for(Account accRecord : accList){
                accMap.put(accRecord.Id, accRecord); /* Putting the Parent Id and Record in Map */
            }
        }
        
        if(!accMap.isEmpty()){
            for(Contact conRecord : conList){ /* Iterating over the Contact and cheking weather the Parent exists in the accMap */
                if(accMap.containsKey(conRecord.AccountId)){
                    if(String.isBlank(accMap.get(conRecord.AccountId).Description)){ /* Checking if the Account's description is blank */
                        accMap.get(conRecord.AccountId).Description = conRecord.Phone; /* If its blank then copying the Child Contacts Phone in the Parent */
                    }
                    else if(!String.isBlank(accMap.get(conRecord.AccountId).Description)){ /* Checking if the Account's description is not blank as there can be multiple children then in the previous iteration of Contacts some Contact might have given its Phone field in the Account's Description */
                        accMap.get(conRecord.AccountId).Description = accMap.get(conRecord.AccountId).Description+' '+conRecord.Phone; /* If Account Description has some data then Concantinate the new Phone with the old Description */
                    }
                }
            }
        update accMap.values(); /* Updating the Parent Account Records in Salesforce Database */
        }
    }
}

// Test Class

@isTest /* Marks that the Class is a Test Class */
private class UpdateAccountDescriptionTest {
    
    @isTest /* Marks that the method is an Apex Unit Test method */
    private static void testWhenContactHasParent(){
        /* Creating the Test Data for this Apex Unit Test method. This data will vanish after this Apex Unit Test methods execution is completed */
        List<Account> accList = new List<Account>(); /* Creating an Empty List to store the Parent Accounts */
        List<Contact> conList = new List<Contact>(); /* Creating an Empty List to store the Child Contact Accounts */
        
        for(Integer i=0; i<20; i++){ /* Creating 20 Account Records */
            Account accRecord = new Account(
                Name ='Parent'+i,
            Description =null); /* Marking the Description as null just to make sure that the Description is null */
            
            accList.add(accRecord); /* Adding the Accounts in accList */
        }
        
        if(!accList.isEmpty()){
            insert accList; /* Commiting the Test Data. This is an Important Step i.e. to always insert the Test Data otherwise it won't be created. The DML operations in a Test Class does not Commit changes to the Salesforce Database */
            
            for(Account accRecord : accList){ /* Iterating over the accList to create 1 Contact for each Parent Account */
                Contact conRecord = new Contact( /* If you want to create multiple contacts for 1 Parent account then there should be an Inner Traditional for loop */
                    LastName = accRecord.Name,
                    Phone ='9921',
                AccountId = accRecord.Id); /* Don't forget to associate the Child Contacts with the Parent Accounts. And before doing this always verify if the Parent Records are Commited or not */
                
                conList.add(conRecord); /* Adding the Contacts to conList */
            }
        }
        
        if(!conList.isEmpty()){
            insert conList; /* Commiting the Child Contacts */
        }
        
        
        Test.startTest(); /* Marks that the Actual beginning Tests i.e. Testing of Apex Class starts here. Any Code between Test.startTest() and Test.stopTest() gets a seperate Set of Governor Limits */
        UpdateAccountDescription test1 = new UpdateAccountDescription(); /* Creating an Instance of the Class whose method we want to test */
        test1.populateAccountDescription(); /* Calling the method that we want to Test */
        Test.stopTest(); /* Marks that the Test has completed here so, any code from below here will have the original governor limits of the Test Class */
        
        /* From here we start using Assert Statements to the check if the Expected == Actual results or not */
        List<Contact> fetchContacts = [SELECT Id, AccountId, Phone FROM Contact WHERE AccountId !=null AND Phone !=null];
        Set<Id> accIdSet = new Set<Id>();
        Map<Id, Account> accMap = new Map<Id, Account>();
        for(Contact conRecord : fetchContacts){
            if(conRecord.AccountId !=null){
                accIdSet.add(conRecord.AccountId);
            }
        }
        if(!accIdSet.isEmpty()){
            List<Account> fetchAccounts = [SELECT Id, Name, Description FROM Account WHERE Id IN: accIdSet];
            for(Account accRecord : fetchAccounts){
                accMap.put(accRecord.Id, accRecord);
            }
        }
        if(!accMap.isEmpty()){
            for(Contact conRecord : fetchContacts){
                if(accMap.containsKey(conRecord.AccountId)){
                    System.assertEquals(conRecord.Phone, accMap.get(conRecord.AccountId).Description); /* Expected Data, Actual Data */
                }
            }
        }
    }
    
    /* In the below Apex Unit Test method we are Testing how our Apex Class performs when the Contacts don't have Parent Accounts. */
    @isTest
    private static void testWhenContactDoesNotHaveParent(){
        List<Contact> conList = new List<Contact>(); 
        
        for(Integer i=0;i<50;i++){
            Contact conRecord = new Contact(
                LastName = 'No Parent'+i,
                Phone ='12345',
                AccountId =null);
            
            conList.add(conRecord);
        }
        
        if(!conList.isEmpty()){
            insert conList; /* Commiting the Contacts */
        }
        
        Test.startTest();
        UpdateAccountDescription test2 = new UpdateAccountDescription();
        test2.populateAccountDescription();
        Test.stopTest();
        
        List<Contact> fetchContacts = [SELECT AccountId, Phone FROM Contact];
        for(Contact conRecord : fetchContacts){
            System.assertEquals(null, conRecord.AccountId);
        }
    }
    
    @isTest
    private static void testWhenTheParentDescriptionIsNotNull(){
        /* Creating the Test Data for this Apex Unit Test method. This data will vanish after this Apex Unit Test methods execution is completed */
        List<Account> accList = new List<Account>(); /* Creating an Empty List to store the Parent Accounts */
        List<Contact> conList = new List<Contact>(); /* Creating an Empty List to store the Child Contact Accounts */
        
        for(Integer i=0; i<20; i++){ /* Creating 20 Account Records */
            Account accRecord = new Account(
                Name ='Parent'+i,
            Description ='Some Data'); /* Marking the Description as null just to make sure that the Description is null */
            
            accList.add(accRecord); /* Adding the Accounts in accList */
        }
        
        if(!accList.isEmpty()){
            insert accList; /* Commiting the Test Data. This is an Important Step i.e. to always insert the Test Data otherwise it won't be created. The DML operations in a Test Class does not Commit changes to the Salesforce Database */
            
            for(Account accRecord : accList){ /* Iterating over the accList to create 1 Contact for each Parent Account */
                Contact conRecord = new Contact( /* If you want to create multiple contacts for 1 Parent account then there should be an Inner Traditional for loop */
                    LastName = accRecord.Name,
                    Phone ='9921',
                AccountId = accRecord.Id); /* Don't forget to associate the Child Contacts with the Parent Accounts. And before doing this always verify if the Parent Records are Commited or not */
                
                conList.add(conRecord); /* Adding the Contacts to conList */
            }
        }
        
        if(!conList.isEmpty()){
            insert conList; /* Commiting the Child Contacts */
        }
        
        Test.startTest();
        UpdateAccountDescription test3 = new UpdateAccountDescription();
        test3.populateAccountDescription();
        Test.stopTest();
        
        /* From here we start using Assert Statements to the check if the Expected == Actual results or not */
        List<Contact> fetchContacts = [SELECT Id, AccountId, Phone FROM Contact WHERE AccountId !=null AND Phone !=null];
        Set<Id> accIdSet = new Set<Id>();
        Map<Id, Account> accMap = new Map<Id, Account>();
        for(Contact conRecord : fetchContacts){
            if(conRecord.AccountId !=null){
                accIdSet.add(conRecord.AccountId);
            }
        }
        if(!accIdSet.isEmpty()){
            List<Account> fetchAccounts = [SELECT Id, Name, Description FROM Account WHERE Id IN: accIdSet];
            for(Account accRecord : fetchAccounts){
                accMap.put(accRecord.Id, accRecord);
            }
        }
        if(!accMap.isEmpty()){
            for(Contact conRecord : fetchContacts){
                if(accMap.containsKey(conRecord.AccountId)){
                    System.assertEquals('Some Data 9921', accMap.get(conRecord.AccountId).Description);
                }
            }
        }
    }
}
```

____
#### 2. Write a Test Class for the Trigger.

```apex
// Trigger

Trigger ContactTrigger on Contact (before insert){
    if(Trigger.isBefore){
        if(Trigger.isInsert){
            ContactTriggerHandler.createAccount(Trigger.new); /* Passing Trigger.new as Argument to the TriggerHandlers method */
        }
    }
}

// TriggerHandler

/* Create a Parent Account if while Creating the Contact the AccountId is Empty */
public class ContactTriggerHandler {
    public static void createAccount(List<Contact> newConList){ /* Parameterized method that needs a Contact List as an Argument */
        Set<String> matchingSet = new Set<String>();
        List<Account> accToCreateList = new List<Account>();
        Map<String, Account> matchingMap = new Map<String, Account>();
        
        for(Contact conRecord : newConList){ /* Iterating the newConList */
            if(conRecord.AccountId ==null){ /* If the Contact does not have a Parent then Create a Parent using its LastName as the Account's Name */
                Account accRecord = new Account(Name =conRecord.LastName);
                accToCreateList.add(accRecord); /* Adding Account to accToCreateList */
                matchingSet.add(conRecord.LastName); /* At the same time adding the Contacts LastName in matchingSet */
            }
        }
        
        if(!accToCreateList.isEmpty()){
            insert accToCreateList; /* Commiting the newly Created Accounts. Till now the Parent and Child are not associated to each other */
            for(Account accRecord : [SELECT Id, Name FROM Account WHERE Name IN: matchingSet]){ /* Using the LastName of Contact we are fetching the Accounts. While creating the Account we could have used fields like Phone or Email to identify the match */
                matchingMap.put(accRecord.Name, accRecord); /* Putting LastName and Account Record in matchingMap */
            }
        }
        
        if(!matchingMap.isEmpty()){
            for(Contact conRecord : newConList){ /* Iterating over Contacts and checking if LastName exists in matchingMap */
                if(matchingMap.containsKey(conRecord.LastName)){
                    conRecord.AccountId = matchingMap.get(conRecord.LastName).Id; /* Associating the Child with Parent */
                }
                /* Here we are not doing DML on Contact Records because we are performing all Operations before the Contact is even commited to Database. And the Contact will get commited automatically because someone is creating the Contact hence, the Trigger was fired */
            }
        }
    }
}

// Test Class

@isTest
public class ContactTriggerTest {
    /* As we know triggers are autolaunched on DML events so, in an Apex Unit Test we only need to do DML events. The Trigger must be active while we run Test. This */
    @isTest
    private static void createTestDataForContactTrigger(){
        
        Test.startTest();
        List<Contact> conList = new List<Contact>();
        for(Integer i=0;i<50;i++){
            Contact conRecord = new Contact(LastName ='Test'+i,
                                            AccountId =null);
            
            conList.add(conRecord);
        }
        
        if(!conList.isEmpty()){
            insert conList;
        }
        Test.stopTest();
        
        List<Contact> fetchContacts = [SELECT AccountId, LastName, Account.Name FROM Contact];
        for(Contact conRecord : fetchContacts){
            Assert.isTrue(conRecord.AccountId !=null); /* Checking if AccountId is null */
            System.assertEquals(conRecord.LastName, conRecord.Account.Name);
        }
    }
}
```

____
#### 3. Write a Test Class for the Batch Apex
**NOTE** : 
1. While Testing Batch Apex do not create more than 2000 records.
2. The Batch size must be equal to the Number of Records that you created as Test Data. Because, in Apex Test Unit Methods we cannot call more that 1 Batch i.e. in Apex Tests only 1 Batch can execute.

```apex
// Batch Apex

/* Fetch Accounts whose Type = Prospect and the Change it to Other */
public class MyBatchClass implements Database.Batchable<sObject>, Database.Stateful { /* Using Database.Stateful so, that variables maintain their state */
    String query = 'SELECT Id, Name, Type FROM Account WHERE Type =\'Prospect\''; /* This is how you can store the query and pass
it later to the Database.getQueryLocator() instead of writing a String in the return statement */
    
    Integer totalAccountsProcessed = 0; /* Storing the Count of Records that got processed */
    
    public Database.QueryLocator start(Database.BatchableContext BC){
        return Database.getQueryLocator(query); /* Passing query to fetch Accounts */
    }
    
    public void execute(Database.BatchableContext BC, List<Account> accList){
        if(!accList.isEmpty()){
            for(Account acc : accList){
                System.debug('Account Name'+acc.Name+', Account Type ='+acc.Type);
            }
        }
        if(!accList.isEmpty()){
            for(Account acc : accList){
                acc.Type ='Other';
            }
            /* instead of dmls you should use Database Class methods in Batch Class because if 1 records fails in dml the entire batch will fail */
            Database.update(accList, false); /* partially updating Account Records in Salesforce Database */
            totalAccountsProcessed = totalAccountsProcessed + accList.size(); /* This is how you add each batches count to the total count */
        }
    }
    
    public void finish(Database.BatchableContext BC){            
    }
}

// Test Class

@isTest
public class MyBatchClassTest {
    
    @isTest
    private static void testBatchClass(){
        List<Account> accList = new List<Account>();
        
        for(Integer i=0;i<2000;i++){ /* Do not make more than 2000 records for testing a Batch Apex because in Apex Unit Test Method you can only call 1 Batch no more than that so, the maximum records you can process is 2000*/
            Account accRecord = new Account(Name ='Test'+i,
                                            Type ='Prospect');
            
            accList.add(accRecord);
        }
        
        if(!accList.isEmpty()){
            insert accList; /* Commiting Records */
        }
        
        Test.startTest();
        MyBatchClass myBatch = new MyBatchClass(); /* So we call the Batch apex like this in the Test Class */
        Id jobId = Database.executeBatch(myBatch, 2000); /* As we have 2000 records in the Test Data we need to use 2000 as batch size because we cannot call more than 1 Batch in Apex Unit Test Method */
        Test.stopTest();
        
        /* Below are Assert Statements */
        List<Account> fetchAccounts = [SELECT Id, Name, Type FROM Account];
        System.assertEquals(2000, fetchAccounts.size());
        for(Account accRecord : fetchAccounts){
            System.assertEquals('Other', accRecord.Type);
        }
    }
}
```

_____
#### 4. Write a Test Class for the Schedulable Apex
_____
#### 5. Write a Test Class for the Queueable Apex
____
#### 6. Write a Test Class for the Future Method
______
