### Things that are applicable to all Asynchronous Apex Types
1. You can run 2,50,000 (2 Lakh 50 Thousand) Asynchronous Processes per 24 Hour OR number of user licenses in your org multiplied by 200, whichever is greater.
2. This Limit is share by Future Method, Batch Apex, Scheduled Apex and Queueable Apex.
3. Reasons why we use Asynchronous Apex
	1. Some limits are higher in Asynchronous Apex. ex: Heap size, Records fetched by `Database.QueryLocator`.
	2. In Sync Apex we call the Async apex ex: `@future` method or Queueable Apex so, that the Process executes on a Separate Thread with different execution Limits.
	3. To run process that can take long time to Complete.
4. As we know that Async means not happening at the Same time. So, if you run a `@future` method, `System.enqueueJob()` a Queueable Apex, `Database.executeBatch()` or `System.scheduleBatch()` a Batch Apex or `System.schedule()` a Schedulable Apex they all go into the Queue and wait for the Resources and then execute. So, even if you have delay set in Queueable Apex or Scheduled the Schedulable Apex the actual execution will only happen when Resources are given to them.
____
## 1. future Method
#### 1.1. What is a future method?
- A future method is just a method in any Apex Code (Sync/Async) which is annoted by `@future`.
- A future method runs in the background, asynchronously i.e. on a seperate thread (counts as a different Transaction).
- Each future method is queued and executes when system resources become available. That way, the execution of your code doesn’t have to wait for the completion of a long-running operation.
- **NOTE** : 
	1. future methods cannot call another future methods i.e. Chaining of Future methods is not possible. You will get `System.AsyncException`: Future method cannot be called from a future or batch method.
	2. Methods with the `@future` annotation must be `static` methods, and can only return a void type.
	3. If the method accepts parameters then the specified parameters must be primitive data types or collections of primitive data types. Methods with the future annotation can’t take sObjects or objects as arguments.
	4. The reason why sObjects can’t be passed as arguments to future methods is because the sObject can change between the time you call the method and the time it executes. In this case, the future method gets the old sObject values and can overwrite them. To work with sObjects that already exist in the database, pass the sObject ID instead (or collection of IDs) as an argument and use the ID to perform a query for the most up-to-date record.
	
___
#### 1.2. When to use future method?
- You can use future method
	1. To make external web callouts.
	2. To run any operation on its seperate Thread (i.e. as a seperate transaction).
	3. To prevent [MIXED_DML_OPERATION error](./11._Apex_Errors) . 
- For external web callouts with `@fututre` you need to write `(callouts=true)` ex: `@fututr(callouts=true)` if you don't write it the the method won't call the external site.
- Ex:
```apex
public class AsyncDemo {
    
    @future(callout=true) /* You can run this block even as Sync i.e. removing the future annotation but the whole point of 
having future method is that sometimes the external site can response very late and hence, you should make web callouts
to external site in a future method */
    
    public static void futureMethod(){
        Http responseObject = new Http();
        HttpRequest myRequest = new HttpRequest();
        myRequest.setEndPoint('https://jsonplaceholder.typicode.com/posts/1'); /* Before this you need to set
the Setup; Remote site settings; Add new Remote site otherwise the code won't run */
        myRequest.setMethod('GET');
        
        HttpResponse gotResponse = new HttpResponse();
        gotResponse = responseObject.send(myRequest);
        
        System.debug(gotResponse.getBody());
    }
}
```
- **NOTE** : 
	1. To monitor the status go to Setup &rarr; Apex Jobs.
	2. As `@future` method is just a method in Apex Class and compulsorily `static` you have to call it directly by the `ClassName.futureMethodName()` in the Execute Anonymous Window.
___
#### 1.3. What are the Governor Limits for future method?
1. You cannot have a `@future` method within a `@future` Method or a Batch Class.
2. 50 Future Methods are allowed per Apex Class or Queueable Apex.
___
## 2. Batch Apex
#### 2.1. What is Batch Apex?
- When an Apex Class implements the `Database.Batchable<sObject>` interface then its called a Batch Apex.
- A Batch class is used to perform operations on large number of records.
- At a Time there can only be 5 Active OR Queued Jobs.  Hence, do not call a Batch Class in a Trigger or you will have more that the allowed Jobs which won't execute.
____
#### 2.2. Explain the different methods of `Database.Batchable<sObject>` Interface.
- As we know that if an Apex class implements an Interface then it must implement all its methods.
- `Database.Batchable` Interface has 3 Methods
	1. `start()` 
		- Needed Arguments - `Database.BatchableContext BC`
		- Return Type - `Database.QueryLocator`
		- Runs - 1 time when the Class is Executed
		- Used - to fetch the Records from the Database using `Database.getQueryLocator(query)`
	2. `execute()`
		- Needed Arguments - `Database.BatchableContext BC` and `List<sObjects> listVariableName`
		- Return Type - `void`
		- Runs - Multiple Times depends on the Number of Records and the Batch Size. Each Batch gets its own separate Async Governor Limits.
		- Used - to perform operations on the Records in multiple batches
	3. `finish()`
		- Needed Arguments - `Database.BatchableContext BC`
		- Return Type - `void`
		- Runs - 1 Time when the Last Batch of `execute()` method is finished
		- Used - to 
			1. send mail to the Admin/ User who executed the Batch
			2. post processing
			3. Chain another Batch Class
____
#### 2.3. How to Run a Batch Class?
You can Run a Batch Job in 2 Ways
1. `Database.executeBatch(Batch Class Instance, Optional Batch Size)`: First you need to create an Instance of the Class and pass that Instance to `Database.execute()` optionally you can also pass a Batch size i.e. number of records to process per batch if you don't pass the default Batch size i.e 200 will be used. The maximum number of Batch size is 2000 Records.
2. `System.scheduleBatch()`
**NOTE** :
- You need to use the 2 Ways in an Apex Sync Class or an Execute Anonymous Window for executing a Batch Class.

```apex
// Batch Class
public class MyBatchClass implements Database.Batchable<sObject>, Database.Stateful { /* Using Database.Stateful so, that variables maintain their state */
    String query = 'SELECT Id, Name, Type FROM Account WHERE Type =\'Other\''; /* This is how you can store the query and pass it later to the Database.getQueryLocator() instead of writing a String in the return statement */
    
    List<Account> fetchedList = new List<Account>(); /* Creating an Empty List to store the Accounts that were fetched by the Database.QueryLocator in start method */
    List<Account> modifiedList = new List<Account>(); /* Creating an Empty List to store the modified values. This is how you will be able to access the modified records in the Finish Method NOTE: It might contain wrong data why? because some records might fail to update and hence, the List might have wrong information */
    
    Integer totalAccountsProcessed = 0; /* Storing the Count of Records that got processed */
    
    public Database.QueryLocator start(Database.BatchableContext BC){
        return Database.getQueryLocator(query); /* Passing query to fetch Accounts */
    }
    
    public void execute(Database.BatchableContext BC, List<Account> accList){
        if(!accList.isEmpty()){
            for(Account acc : accList){
                fetchedList.add(acc);
                System.debug('Account Name'+acc.Name+', Account Type ='+acc.Type);
            }
        }
        if(!accList.isEmpty()){
            for(Account acc : accList){
                acc.Type ='Prospect';
                modifiedList.add(acc); /* Adding the Updated Records to modified List to access in the finish method */
            }
            /* instead of dmls you should use Database Class methods in Batch Class because if 1 records fails in dml the entire batch will fail */
            Database.update(accList, false); /* partially updating Account Records in Salesforce Database */
            totalAccountsProcessed = totalAccountsProcessed + accList.size(); /* This is how you add each batches count to the total count */
        }
    }
    
    public void finish(Database.BatchableContext BC){
        
        AsyncApexJob a = [SELECT Id, Status, NumberOfErrors, JobItemsProcessed,
                          TotalJobItems, CreatedBy.Email
                          FROM AsyncApexJob WHERE Id =
                          :BC.getJobId()]; /* Did this soql to send the Details via Email */
        
        /* Here you can send mail to the Admin or the list of mails that we might have in the modified List */
        Messaging.SingleEmailMessage mail = new Messaging.SingleEmailMessage();
        String[] toAddresses = new String[] {'temp@mail.com',a.CreatedBy.Email};
            mail.setToAddresses(toAddresses);
        mail.setSubject('Apex Status ' + a.Status);
        mail.setPlainTextBody
            ('Total Number of Batches ='+a.TotalJobItems+
             ' Total Number of Errors ='+a.NumberOfErrors + ' Total Number Records Processed ='+a.JobItemsProcessed +', Value of my Variable ='+totalAccountsProcessed);
        Messaging.sendEmail(new Messaging.SingleEmailMessage[] { mail });        
    }
}

// To Run
MyBatchClass batchObject = new MyBatchClass();
Id jobId = Database.executeBatch(batchObject,1); /* This is how you can save the Job Id */
System.debug('Your Job Id is ='+jobId);

// Running using a Sync Class

```
- **NOTE** : 
	1. We are executing the Batch Apex in Execute Anonymous Window but still the Batch Class Runs in System Mode.
	2. To view status/abort a Running Batch Job go to Setup &rarr; Apex Jobs.
	3. The Batch size that you pass to `Database.executeBatch` is Optional and depends on how complex operations you are doing and what's the Total Number of Records that you are working with.
	4. You can use the Job Id in `System.abortJob(jobId)` to abort a Job that's in a Queue. If you `System.abortJob('jobId as String')` a Batch Job that has completed some batches the changes made by those batches are committed and won't roll back.
	5. If you want to make callouts to external sites from Batch Apex the you also need to implement `Database.AllowsCallouts` Interface. This Interface does not have any method so, don't need to worry about anything else.
	6. The Variables of the Batch Class loose their State at the end of the execution of each batch hence, Implement `Database.Stateful` interface to maintain the State of the Variables.
____
#### 2.4. What are the Governor Limits for Batch Apex?
- At a Time there can be maximum 5 Batch Jobs in the Queue.
- All 5 can be active at a Time.
- But a Time only 1 `start()` method of the 5 Jobs can run. Ex: You started executing Batch Classes using `Database.executeBatch(InstanceName, BatchSize)` so, all of them will be active but `start()` method will work 1 by 1 for all those 5. All 5 Batches `start()` method won't work at the same time. 
- Batch jobs that haven’t started yet remain in the queue until they’re started.
- When batch jobs are submitted, they’re held in the flex queue before the system queues them for processing.
- The `start()` method has return type `Database.QueryLocator()` which can fetch up to 50 Million Records i.e. 500 Lakh Records. If more than 50 million records are returned, the batch job is immediately terminated and marked as Failed.
- Using `FOR UPDATE` in SOQL queries to lock records during update isn’t applicable to Batch Apex.
____
#### 2.5. Apex Jobs Queue vs Apex Flex Queue.

| Apex Jobs Queue                                                     | Apex Flex Queue                                                                                                                                                                                                                                                                                                                                                                                                                                                    |
| ------------------------------------------------------------------- | ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------ |
| At a Time there can be 5 Batch Jobs Active/Queued in the Apex Jobs. | 1. If you add any Bacth Job above the limit i.e. 5 Batch Jobs then the rest of the Batch Jobs are Queued in the Apex Flex Queue.<br/>2. As soon as the Apex Jobs Queue has space the Batch Jobs from Apex Flex Queue are moved to the Apex Jobs Queue.<br/>3.  You can reorder the Apex Batch Jobs in the Apex Flex Queue based on the order in which you want to prioritise their execution.<br/>4. At a Time there can be 100 Batch Jobs in the Apex Flex Queue. |

- **NOTE** : The Apex Flex Queue is only used for the Apex Batch Jobs.

- **Status of Batch Jobs**

| Status     | Description                                                                                                                        |
| ---------- | ---------------------------------------------------------------------------------------------------------------------------------- |
| Holding    | Apex Batch Job is in Apex Flex Queue. This is the only status that happens in the Apex Flex Queue rest happens in Apex Jobs Queue. |
| Queued     | Apex Batch Job is in Apex Jobs Queue and will process when resources are available.                                                |
| Preparing  | The `start()` method has been invoked.                                                                                             |
| Processing | Apex Batch Job is running i.e. past `start()` method.                                                                              |
| Aborted    | The Apex Batch Job was aborted by the User.                                                                                        |
| Completed  | The Apex Batch Job finished executing.                                                                                             |
| Failed     | Job experienced a system failure.                                                                                                  |

___
## 3. Queueable Apex
#### 3.1. What is a Queueable Apex?
- When an Apex Class implements the `Queueable` interface then its called a Queueable Apex.
- Queueable Apex performs the same things that we perform in `@future` methods but Queueable Apex gives you a way to monitor the Jobs.

| `@future` method                                                                                                                                                                             | Queueable Apex                                                                                                                                                                                                         |
| -------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------- | ---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------- |
| 1. Does not return a jobId.<br/>2. Does not support chaining of future methods.<br/>3. Only accepts primitive parameters or collections of primitive data types.<br/>4. Cannot be scheduled. | 1. Returns a jobId just like the Batch Apex.<br/>2. You can Chain Queueable Apex.<br/>3. Can Contain non-primitive data types.<br/>4. Cannot be Scheduled but can execution can be delayed by providing  a delay time. |

____
#### 3.2. Explain the methods of `Queueable` Interface.
- As we know that if an Apex class implements an Interface then it must implement all its methods.
- `Queueable` Interface has only 1 Method.
- `execute():`
	- Needed Arguments - `QueueableContext QC`
	- Return Type - `void`
	- Runs - Multiple Times depends on the Number of Records and the Batch Size
	- Used -
		1. To make external web callouts.
		2. To run any operation on its seperate Thread (i.e. as a seperate transaction).
___
#### 3.3. How to Run a Queueable Apex
- You need to use the Execute Anonymous Window for executing a Queueable Apex.
- We use `System.enqueueJob()`.
- First you need to create an Instance of the Class and pass that Instance to `Database.execute()` optionally you can also pass a Delay Tim e in Minutes.
- The maximum Delay can be 10 minutes.

```apex
public class QueueableApexClass implements Queueable, Database.AllowsCallouts {
    /* Database.AllowsCallouts interface must be implemented if you want to make callounts from a
Queueable Apex or a Batch Apex otherwise you won't be able to make callouts to external sites */
    
    public void execute(QueueableContext QC){
        Http responseObject = new Http();
        HttpRequest myRequest = new HttpRequest();
        myRequest.setEndPoint('https://jsonplaceholder.typicode.com/posts/1'); /* Before this you need to set
the Setup; Remote site settings; Add new Remote site otherwise the code won't run */
        myRequest.setMethod('GET');
        
        HttpResponse gotResponse = new HttpResponse();
        gotResponse = responseObject.send(myRequest);
        
        System.debug(gotResponse.getBody());
    }
}

// To Run
QueueableApexClass myJob = new QueueableApexClass();
Id jobId = System.enqueueJob(myJob,0); /* The delay can be from 0 minutes to 10 minutes */
System.debug(jobId);
```
- **NOTE** : 
	1. To view status/abort a Running Batch Job go to Setup &rarr; Apex Jobs.
	2. You can use the Job Id in `System.abortJob(jobId)` to abort a Job that's in a Queue. 
	3. If you want to make callouts to external sites from Queueable Apex the you also need to implement `Database.AllowsCallouts` Interface. This Interface does not have any method so, don't need to worry about anything else.
____
#### 3.4. What are the Governor Limits for Queueable Apex?
- You can call 50 `@future` methods in a Queueable Apex.
- From a Sync Apex you can call 50 Queueable Apex.
- When chaining jobs with `System.enqueueJob` at the Bottom of the `execute()` method, you can add only one job from an executing job. Only one child job can exist for each parent Queueable job. Starting multiple child jobs from the same Queueable job isn’t supported.
- Because no limit is enforced on the depth of chained jobs, you can chain one job to another. You can repeat this process with each new child job to link it to a new child job. For Developer Edition and Trial organizations, the maximum stack depth for chained jobs is 5, which means that you can chain jobs four times. The maximum number of jobs in the chain is 5, including the initial parent Queueable job.
___
## 4. Schedulable Apex
#### 4.1. What is a Schedulable Apex?
- When an Apex Class implements the `Schedulable` interface then its called a Schedulable Apex.
- So, the only purpose of `Schedulable` Apex is to Schedule the Execution Time.
____
#### 4.2. Explain the methods of `Schedulable` Interface.
As we know that if an Apex class implements an Interface then it must implement all its methods.
- `Schedulable` Interface has only 1 Method.
- `execute():`
	- Needed Arguments - `SchedulableContext SC` 
	- Return Type - `void`
	- Used - To schedule an Apex Class.
____
#### 4.3. How to Run a `Schedulable` Apex?
**NOTE** : Whenever you Schedule a `Schedulable` Apex you need to give it a Custom Job Name.
You can Schedule a `Schedulable` Apex in 2 ways:
1. Configuration : Go to Setup &rarr; Apex Classes &rarr; Schedule Apex &rarr; Give a Job Name &rarr; Select Apex Class (`Schedulable` Apex Class) &rarr; Frequency (Weekly/ Monthly)(All Days or Specific Days) &rarr; Select Start Time and End Time &rarr; Preferred Start Time.
2. Configuration : You need to use the Execute Anonymous Window for executing a Queueable Apex.
	- We use `System.schedule('customJobNameVariable',cronExpression)`.
	- First you need to create an Instance of the Class and pass that Instance to `System.schedule()`.
- **NOTE** :
	1. To monitor go to Setup &rarr; Scheduled Jobs.
____
#### 4.4. What are the Governor Limits for Schedulable Apex?
- 100 Apex Jobs can be scheduled at a Time.
- Though it's possible to do Processing/Business Logic in `execute()` method but it's recommended that you should have logic in a different class and just create the Instance of that Class in the `execute()` method.
____
# Extra Info
#### 1. When to use which Async Apex Type

| Async Type                                                               | When to Use      |
| ------------------------------------------------------------------------ | ---------------- |
| To only do web callouts                                                  | `@future` method |
| To process a lot of Data in Batches                                      | Batch Apex       |
| For Chaining (i.e. must execute in a Order)                              | Queueable Apex   |
| Execute Apex at Specific Time Intervals (Repeated or Once but at a Time) | Schedulable Apex |
- **NOTE**:
	1. You can still do Web callouts in Queueable Apex, Batch Apex and Schedulable Apex if you implement `Databse.AllowsCallouts` Interface.
	2. `@future` must be `static` and for the Rest of the Async Types the Interfaces Methods must be `non-static`.
	3. All Interface methods must be public or global.
___
#### 2. From Where you can call Which Async Apex Type
1. Batch Apex 
	- Can be Called From
		1. Any method of a Sync Apex Class
		2. `finish()` method of another Batch Class i.e. Batch Chaining
		3. `execute()` method of a Queueable Apex.
		4. `execute()` method of a Schedulable Apex.
	- Cannot be Called From
		1. `@future` method
		2. `start()` or `execute()` method of Another Batch Apex.
2. `@future` Method
	- Can be Called From
		1. Annotate any Method as `@future` in a Sync Apex.
		2. `execute()` method of a Queueable Apex.
	- Cannot be called from
		1. Another `@future` Method
		2. From any method of a Batch Class
3. Queueable Apex
	- Can be Called From anywhere
		1. Any method of a Sync Apex Class
		2. Any `@future` Method
		3. `finish()` method of a Batch Class. But Limit is 1 only.
		4. `execute()` method of another Queueable Apex. But Limit is 1 only.
		5. `execute()` method of a Schedulable Apex.
4. Schedulable Apex
	- Can be Called From
**NOTE** : You can implement the `Database.Batchable<sObject>`, `Queueable` and `Schedulable` interface on a single class because, a Class can implement multiple Interfaces but this doesn't mean its the correct way. What you should do is that you should implement the `Schedulable` interface on 1 Apex and from that you should call the Batch Apex. This will seperate the code properly.
____
 
