## Exception Handling
#### 1. What are Exceptions in Apex?
1. Exceptions are Errors which happens after the Code is Compiled successfully i.e. Exceptions are not Syntactic Error they are Logical Errors.
2. Apex uses exceptions to note errors and other events that disrupt the normal flow of code execution.
3. Example : 

```apex
public class ExceptionHandling {
    /* All variables are declared static because my display() method is static and non-static variables cannot be used in static methods */
    static Integer a; /* I have not assigned any value to a hence, it's null */
    static Integer b = 10;
    
    static Integer sum = a+b; /* You cannot add an integer to a null variable */
    
    
    /* NOTE : The Apex class will compile but when you run the below method it will give an Exception : Line: 6, Column: 1
System.NullPointerException: Attempt to de-reference a null object
 */
    public static Integer display(){
        return sum;
    }
}
```
____
#### 2. What is Exceptional Handling?
- When an exception occurs, code execution halts. Any DML operations that were processed before the exception are rolled back and aren’t committed to the database. Exceptions get logged in debug logs.
- So, Exception Handling is a Methodology in which we use `try`, `catch` and `finally` Code blocks and what it does is that it will Handle the Exception and will let the rest of code run fine i.e. rest of the Code is Executed.
____
#### 3. Explain `try`, `catch` and `finally` Blocks.
1. `try`
	1. In `try` code block we write the Piece of code which is throwing exception or we expect that there could be an exception.
	2. Ex: While you are using `dml` statement on a List some records might give errors so, writing `dml` statement in `try` block is good practice.
	3. Any statements in the try block occurring after the exception are skipped and aren’t executed. Ex: `dml insert` is throwing `System.DmlException`: Insert failed because of `REQUIRED_FIELD_MISSING` so, lines that you wrote below `dml insert` will be skipped.
2. `catch`
	1. A `catch` block is used to handle the Exception which is thrown in the `try` block. 
	2. A `try` block may throw multiple exceptions so, for 1 `try` block there can be multiple `catch` blocks. 
	3. The catch block must have a Parameter whose data type will be the `exceptionType` and any variable name that you want to give it ex: `catch(Exception e)`.
	4. Once a particular exception type is caught in one catch block, the remaining catch blocks, if any, aren’t executed.
3. `finally`
	1. Any code which is written in the `finally` block is guaranteed to execute i.e. no matter if `try` block threw an Exception or not the `finally` will always execute.
	2. Because the finally block always executes, use it for cleanup code, such as for freeing up resources.

```apex
// Syntax
try {
    // Perform some operation that 
    //   might cause an exception.
} catch(Exception e) {
    // Generic exception handling code here.
} finally {
    // Perform some clean up.
}

// Example
public class ExceptionHandling {
    /* All variables are declared static because my display() method is static and non-static variables cannot be used in static methods */
    static Integer a; /* I have not assigned any value to a hence, it's null */
    static Integer b = 10;
    
    /* NOTE : We are handling the Exception hence, the code will execute */
    public static void display(){
        try{
            Integer sum = a+b; /* This will throw System.NullPointerException: Attempt to de-reference a null object i.e. variable a */
            Integer sub = a-b; /* This doesn't execute since sum causes an exception and all lines after the exception is Thrown are ignored. So, this statement should not be here it should have its own try block after the finally block of this try block */
        } catch(Exception e){ /* Exception is a generic Exception Data Type so the variable e will get the Exception */
            /* So, in the catch block we write what should happen if we get an Exception */
            System.debug('The Exception was ='+e);
        }finally{
            /* Code in the finally block will alwyas run */
            System.debug('Finally is always executed');
        }
    }
}
```

- **NOTE** : 
	1. If you have written a `try` block then for that `try` block must be Succeeded by a `catch` block or a `finally` block.
	2. You can have multiple `try` blocks but they all must have their own `catch` or `finally` blocks.
____
#### 4. Can you handle all types of Exceptions?
1. No, there are some special Exceptions that we cannot handle.
2. Ex: `System.LimitException` that the runtime throws if a governor limit has been exceeded, such as when the maximum number of SOQL queries issued has been exceeded.
____
#### 5. What are the Different Built-In Exceptions?
A Built-In Exception is a Exception Data Type that we use to create an Exception Variable as a Parameter for the `catch` block.

| Exception                     | Description                                                                                                                                  |
| ----------------------------- | -------------------------------------------------------------------------------------------------------------------------------------------- |
| `System.NullPointerException` | This is thrown when you are trying to perform operations on a Variable or Collection which is `null`.                                        |
| `System.MathException`        | This is thrown when you have mathematical errors ex: trying to divide a number by 0 and Apex does not support Infinity hence, MathException. |
| `System.ListException`        | This is thrown when you have problems with List ex: `accList.get(100);` just imagine the List only has 3 items in it.                        |
| `System.LimitException`       | This is thrown when your Governor Limits are exceeded. **NOTE** : This Exception can't be caught.                                            |
| `System.DmlException`         | This is thrown when you have problems with a DML statement, such as an `dml insert` statement missing a required field on a record.          |
| `System.QueryException`       | This is thrown when you have SOQL that returns no records or more than one record to a singleton sObject variable.                           |
| `System.FinalException`       | This is thrown when you are trying to modify a read only type ex: Variables that are declared with `final` keyword.                          |
| `System.HandledException`     | This is thrown when you are using `Exception` as a data type for the `catch` block.                                                          |
| `System.AsyncException`       | This is thrown whenever there is an error while executing an Async Apex ex:                                                                  |
| `System.CalloutException`     | This is thrown whenever there is an error while making callouts to external systems.                                                         |

____
#### 6. What are Exception Methods?
Exception Methods are methods that are available on the Exception object that we had as Parameter in `catch` block.

| Exception Methods | Description                                                                                   |
| ----------------- | --------------------------------------------------------------------------------------------- |
| `getCause`        | Returns the cause of the exception as an exception object.                                    |
| `getMessage`      | Returns the error message that displays for the user.                                         |
| `getLineNumber`   | Returns the line number from where the exception was thrown.                                  |
| `getTypeName`     | Returns the type of exception, such as DmlException, ListException, MathException, and so on. |

```apex
public class ExceptionHandling {
    
    
    public static void operate(){
        Account acc = new Account(
            Rating = 'Hot');
        
        try{
           insert acc; /* This will Throw exception becuase a Required Field i.e. Name is not provided in acc record */
        }catch(Exception e){
            System.debug('getCause ='+e.getCause()); /* RETURNS : null */
            System.debug('getMessage ='+e.getMessage()); /* RETURNS : Insert failed. First exception on row 0; first error: REQUIRED_FIELD_MISSING, Required fields are missing: [Name]: [Name] */
            System.debug('getTypeName ='+e.getTypeName()); /* RETURNS : System.DmlException */
            System.debug('getLineNumber ='+e.getLineNumber()); /* RETURNS : 9 */
        }
        
    }
}
```

____
#### 7. How to handle `DmlException`?
When we use `DmlException` for the Variable as a Parameter for `catch()` then the Variable  gets some special Methods which is only available while using `DmlException`.

```apex
public class ExceptionHandling {
    public static void dmlException(){
        Integer recordIndex =0;
        List<Account> accList = New List<Account>();
        
        for(Integer i =0;i<3;i++){
            Account acc = new Account(
                Name ='Exception Test'+i);
            accList.add(acc);
        }
        
        for(Integer i =0;i<3;i++){
            Account acc = new Account(
                Rating ='Warm');
            accList.add(acc);
        }
        
        for(Integer i =0;i<3;i++){
            Account acc = new Account(
                Name ='Exception Test Records'+i);
            accList.add(acc);
        }
        
        for(Integer i =0;i<3;i++){
            Account acc = new Account(
                Rating ='Warm');
            accList.add(acc);
        }
        
        
        System.debug('Printing List Data along with its Index Number');
        for(Account acc : accList){
            System.debug(acc+' Index number ='+recordIndex);
            recordIndex++;
        }
        
        System.debug('Printing the DmlException Handling');
        try{
            insert accList;
        }catch(DmlException de){
            Integer numErrors = de.getNumDml();
            for(Integer i=0;i<numErrors;i++) {
                System.debug('Which field caused error =' + de.getDmlFieldNames(i));
                System.debug('What is the Error Msg =' + de.getDmlMessage(i));  
                System.debug('Whats the Record Id of the Record which caused the error =' +de.getDmlId(i));
                System.debug('Whats the dml error ='+ de.getDmlType(i));
            }
        }
    }
}
```

____
#### 8. When should you use Exception Handling?
1. When you are getting an Exception then you have to either solve the Exception Error or use Exception Handling to check the rest of the code.
2. If you are using `dml` statements instead of  Database Class methods in a Batch Apex then you should definitely use exception handling to check why the entire batch failed.
___
## Database Class Methods
#### 1. What are Database Class Methods?

#### DML Statements vs. Database Class Methods

|DML statements|Database Class Methods|
|---|---|
|1. If 1 record has error then all records in the transaction will be rolled back to the previous state i.e. nothing will be committed.  <br>2. The Exception needs to handled in try-catch.|1. You can choose if you want the Database operation to behave like the DML statement or you can can have Partial Record Processing.  <br>2. The Database class has `Database.SaveResult[]` which helps you handle the exceptions.|


- If we have a long List then some records might be missing Required Fields or they might have some issue with the Validation rules and if such thing happens and if you are using DML statements then the entire operation will be Rolled Back i.e. no records will be committed.
- Apex has a Built in Class called Database Class.
- Database Class contains methods that can perform DML operations.
- Database class methods are static and hence, called through the class name ex: `Database.insert();`
- So, basically Database class also, performs DML operations the only difference is that you can commit partial List of records using Database class while DML allowed the complete List only no partial commits.
- NOTE: In partial commit no exception will be thrown.
- The Database methods returns result objects containing success or failure information of each record.
	- for Insert and Update: `Database.SaveResult results[] = Database.insert(accList, false);`
	- for upsert : `Database.UpsertResult results[] = Database.upsert(accList, false);`
	- for delete : `Database.DeleteResult results[] = Database.delete(accList, false);`

```apex
public class UsingDatabaseClass {
    List<Account> accList = new List<Account>();
    public void populate(){
        Account acc = new Account(
            Name ='Exception');
        accList.add(acc);
        
        Account acc2 = new Account(
            Rating ='Hot');
        accList.add(acc2);
        
        Database.SaveResult[] resultList = Database.insert(accList, false);
        
        for(Database.SaveResult result : resultList){
            if(result.isSuccess()){
                System.debug('Successful ID = '+result.getId());
            }
            else{
                for(Database.Error error : result.getErrors()){
                    System.debug('Error Code = '+error.getStatusCode()+' Field = '+error.getFields()+' Message = '+error.getMessage());
                }
            }
        }
    }
}
```

___
